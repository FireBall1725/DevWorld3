buildscript {
    repositories {
        maven { url = 'https://repo.u-team.info' }
    }
    dependencies {
        classpath 'info.u-team.curse_gradle_uploader:curse_gradle_uploader:1.6.0'
        classpath 'com.github.breadmoirai:github-release:2.3.7'
    }
}

plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'com.github.breadmoirai.github-release' version '2.3.7'
}

apply plugin: 'info.u_team.curse_gradle_uploader'

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group

    // Version from CI_VERSION env var, git tags, or fallback to gradle.properties
    if (System.getenv("CI_VERSION")) {
        version = System.getenv("CI_VERSION")
    } else {
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags', '--abbrev=0'
                standardOutput = stdout
                ignoreExitValue = true
            }
            def gitVersion = stdout.toString().replace("\n", "").replace("\r", "").trim()
            version = gitVersion.isEmpty() ? rootProject.mod_version : gitVersion
        } catch (Exception e) {
            version = rootProject.mod_version
        }
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}

// CurseForge publishing for multi-platform artifacts
curseforge {
    apiKey = System.getenv('CURSE_API_KEY') ?: ''

    // Fabric artifact
    project {
        id = project.hasProperty('curse_project_id') ? curse_project_id : ''
        changelog = file('CHANGELOG.md')
        releaseType = project.hasProperty('curse_release_type') ? curse_release_type : 'release'

        addGameVersion '1.21.1'
        addGameVersion 'Fabric'

        mainArtifact(file("fabric/build/libs/devworld-fabric-${project.version}.jar")) {
            displayName = "DevWorld ${project.version} (Fabric)"
        }
    }

    // NeoForge artifact
    project {
        id = project.hasProperty('curse_project_id') ? curse_project_id : ''
        changelog = file('CHANGELOG.md')
        releaseType = project.hasProperty('curse_release_type') ? curse_release_type : 'release'

        addGameVersion '1.21.1'
        addGameVersion 'NeoForge'

        mainArtifact(file("neoforge/build/libs/devworld-neoforge-${project.version}.jar")) {
            displayName = "DevWorld ${project.version} (NeoForge)"
        }
    }
}

// GitHub Release publishing
String readChangelogString(String filePath) {
    File file = new File(filePath)
    if(file.exists()) {
        return file.text
    }
    return ""
}

githubRelease {
    token System.getenv("GITHUB_TOKEN") ?: "InvalidP@ssword"
    owner System.getenv("CIRCLE_PROJECT_USERNAME") ?: ""
    repo System.getenv("CIRCLE_PROJECT_REPONAME") ?: ""
    targetCommitish = System.getenv("CIRCLE_SHA1") ?: ""
    releaseAssets file("fabric/build/libs").listFiles(), file("neoforge/build/libs").listFiles()
    body {
        return readChangelogString("CHANGELOG.md")
    }
}
